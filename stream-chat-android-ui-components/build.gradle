import io.getstream.chat.android.Configuration
import io.getstream.chat.android.Dependencies

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'de.mannodermaus.android-junit5'

ext {
    PUBLISH_GROUP_ID = Configuration.artifactGroup
    PUBLISH_ARTIFACT_ID = 'stream-chat-android-ui-components'
    PUBLISH_VERSION = rootVersionName
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android.gradle"

android {
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }

    resourcePrefix 'stream_ui_'

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
            // Show the result of every unit test, even if it passes.
            all {
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
        }
    }

    sourceSets {
        all {
            it.java.srcDir "src/$it.name/kotlin"
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-progressive',
                '-Xexplicit-api=strict',
                '-opt-in=kotlin.contracts.ExperimentalContracts',
                '-opt-in=io.getstream.chat.android.core.internal.InternalStreamChatApi',
                '-opt-in=io.getstream.chat.android.core.ExperimentalStreamChatApi',
        ]
    }
}

dependencies {
    api project(":stream-chat-android-ui-common")
    implementation project(":stream-chat-android-ui-utils")

    implementation Dependencies.streamLog
    implementation Dependencies.coroutinesCore
    implementation Dependencies.androidxActivityKtx
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidLegacySupport
    implementation Dependencies.constraintLayout
    implementation Dependencies.materialComponents
    implementation Dependencies.androidxFragmentKtx
    implementation Dependencies.androidxViewPager2
    implementation Dependencies.androidxCoreKtx
    implementation Dependencies.photoview
    implementation Dependencies.coil
    implementation Dependencies.coilGif
    implementation Dependencies.coilVideo
    implementation Dependencies.androidxStartup
    implementation Dependencies.stfalconImageViewer
    implementation Dependencies.androidxLifecycleLiveDataKtx
    implementation Dependencies.keyboardVisibilityEvent
    implementation Dependencies.permissionx

    // Tests
    testImplementation project(":stream-chat-android-test")
    testImplementation testFixtures(project(":stream-chat-android-core"))
    testImplementation Dependencies.junitJupiterApi
    testImplementation Dependencies.junitJupiterParams
    testRuntimeOnly Dependencies.junitJupiterEngine
    testRuntimeOnly Dependencies.junitVintageEngine
    testImplementation Dependencies.kluent
    testImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.androidxArchCoreTest

    detektPlugins(Dependencies.detektFormatting)
}
