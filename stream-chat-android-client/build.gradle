import com.getstream.sdk.chat.Dependencies
import com.getstream.sdk.chat.Configuration

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "com.hiya.jacoco-android"

group = Configuration.artifactGroup
version = Configuration.versionName

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName Configuration.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'STREAM_CHAT_CLIENT_VERSION', "\"$version\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
    }

    resourcePrefix 'stream_'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions.unitTests {
        returnDefaultValues = true
    }

    kotlinOptions {
        jvmTarget = '1.8'
        noStdlib = true
    }

    lintOptions {
        xmlReport false
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-progressive',
                '-Xexplicit-api=strict',
                '-Xopt-in=kotlin.RequiresOptIn',
                '-Xopt-in=io.getstream.chat.core.internal.InternalStreamChatApi',
        ]
    }
}

dependencies {
    implementation project(':stream-chat-android-core')

    implementation Dependencies.kotlinStdLib
    implementation Dependencies.kotlinReflect

    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidxCoreKtx
    implementation Dependencies.androidxLifecycleExtensions
    implementation Dependencies.constraintLayout
    implementation Dependencies.coroutinesCore
    implementation Dependencies.firebaseMessaging
    implementation Dependencies.retrofit
    implementation Dependencies.retrofitGsonConverter
    implementation Dependencies.gson
    implementation Dependencies.okhttpLoggingInterceptor
    implementation Dependencies.ok2curl
    debugImplementation Dependencies.flipper
    debugImplementation Dependencies.flipperNetwork
    debugImplementation Dependencies.flipperLoader

    // Tests
    testImplementation Dependencies.androidxTestJunit
    testImplementation Dependencies.junitJupiterApi
    testImplementation Dependencies.junitJupiterParams
    testRuntimeOnly Dependencies.junitJupiterEngine
    testRuntimeOnly Dependencies.junitVintageEngine

    testImplementation Dependencies.assertj
    testImplementation(Dependencies.awaitility) {
        exclude group: "org.hamcrest", module: "hamcrest"
    }
    testImplementation Dependencies.json
    testImplementation Dependencies.kluent
    testImplementation Dependencies.kfixture
    testImplementation Dependencies.mockito
    testImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.okhttpMockWebserver
    testImplementation Dependencies.robolectric

    // Instrumentation tests
    androidTestImplementation Dependencies.assertj
    androidTestImplementation(Dependencies.awaitility) {
        exclude group: "org.hamcrest", module: "hamcrest"
    }
    androidTestImplementation Dependencies.junit4
    androidTestImplementation Dependencies.espressoCore
    androidTestImplementation Dependencies.androidxTestJunit
}
