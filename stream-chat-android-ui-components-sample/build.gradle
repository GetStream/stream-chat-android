import io.getstream.chat.android.Dependencies

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.huawei.agconnect'
apply plugin: 'shot'

apply from: "${rootDir}/scripts/android.gradle"

android {
    defaultConfig {
        applicationId "io.getstream.chat.ui.sample"
        versionCode sampleAppVersionCode
        versionName sampleAppVersionName
        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
        testApplicationId "io.getstream.chat.ui.sample"
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    File signFile = rootProject.file('.sign/keystore.properties')
    if (signFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signFile))
        signingConfigs {
            release {
                storeFile rootProject.file(properties['keystore'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    } else {
        signingConfigs {
            release {
                storeFile rootProject.file('.sign/debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }

    signingConfigs.debug {
        storeFile rootProject.file('.sign/debug.keystore')
        storePassword 'android'
        keyAlias 'androiddebugkey'
        keyPassword 'android'
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "version"
    productFlavors {
        demo {
            dimension "version"
        }
        full {
            dimension "version"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == "debug" && names.contains("full")) {
            setIgnore(true)
        }
    }

    lintOptions {
        abortOnError false
        xmlReport true
        checkDependencies true
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    buildFeatures {
        viewBinding true
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-Xopt-in=kotlin.RequiresOptIn',
        ]
    }
}

shot {
    applicationId = "io.getstream.chat.ui.sample"
}

repositories {
    // huawei maven
    maven {
        url 'https://developer.huawei.com/repo/'
        content {
            includeGroup("com.huawei.agconnect")
            includeGroup("com.huawei.android.hms")
            includeGroup("com.huawei.hms")
            includeGroup("com.huawei.hmf")
        }
    }
}

dependencies {
    implementation project(":stream-chat-android-ui-components")
    implementation project(":stream-chat-android-markdown-transformer")
    implementation project(":stream-chat-android-pushprovider-firebase")
    implementation project(":stream-chat-android-pushprovider-huawei")
    implementation project(":stream-chat-android-pushprovider-xiaomi")
    implementation files('libs/MiPush_SDK_Client_4_8_2.jar')

    implementation Dependencies.androidxActivityKtx
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidxCoreKtx
    implementation Dependencies.androidxLifecycleLiveDataKtx
    implementation Dependencies.androidxPreferences
    implementation Dependencies.constraintLayout
    implementation Dependencies.androidxFragmentKtx
    implementation Dependencies.materialComponents

    implementation Dependencies.navigationRuntimeKTX
    implementation Dependencies.navigationFragmentKTX
    implementation Dependencies.navigationUIKTX

    implementation Dependencies.firebaseAnalytics
    implementation Dependencies.firebaseCrashlytics

    implementation Dependencies.coil
    implementation Dependencies.coilGif
    implementation Dependencies.shimmer

    debugImplementation Dependencies.flipper
    debugImplementation Dependencies.flipperNetwork
    debugImplementation Dependencies.flipperLoader
    debugImplementation Dependencies.fragmentTest

    // Instrumentation tests
    androidTestImplementation Dependencies.junit4
    androidTestImplementation Dependencies.espressoCore
    androidTestImplementation Dependencies.androidxTestJunit
    androidTestImplementation Dependencies.navigationTest
}
