import io.getstream.chat.android.Dependencies
import io.getstream.chat.android.Configuration

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'androidx.baselineprofile'

ext {
    PUBLISH_GROUP_ID = Configuration.artifactGroup
    PUBLISH_ARTIFACT_ID = 'stream-chat-android-offline'
    PUBLISH_VERSION = rootVersionName
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android.gradle"

android {
    namespace "io.getstream.chat.android.offline"
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions.unitTests {
        includeAndroidResources = true
        returnDefaultValues = true
        all {
            testLogging {
                events 'failed', 'standardOut', 'standardError'
                showExceptions true
                showCauses true
                showStackTraces true
                exceptionFormat 'full'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

baselineProfile {
    baselineProfileOutputDir = "."
    filter {
        include("io.getstream.chat.android.offline.**")
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-progressive',
                '-Xexplicit-api=strict',
                '-opt-in=kotlin.RequiresOptIn',
                '-opt-in=io.getstream.chat.android.core.internal.InternalStreamChatApi',
        ]
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    api project(":stream-chat-android-client")

    implementation Dependencies.streamLog
    // Kotlin
    implementation Dependencies.kotlinReflect
    implementation Dependencies.coroutinesCore

    // Google libs
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidxCoreKtx
    implementation Dependencies.androidxLifecycleLiveDataKtx
    implementation Dependencies.roomRuntime
    implementation Dependencies.roomKtx
    ksp Dependencies.roomCompiler
    implementation Dependencies.workRuntimeKtx

    // Serialization
    implementation Dependencies.moshi
    implementation Dependencies.moshiKotlin
    implementation Dependencies.moshiAdapters
    ksp Dependencies.moshiCodegen

    // Tests
    testImplementation project(":stream-chat-android-test")
    testImplementation project(":stream-chat-android-client-test")
    testImplementation testFixtures(project(":stream-chat-android-core"))
    testImplementation Dependencies.streamResult
    testImplementation Dependencies.moshiKotlin

    testImplementation Dependencies.junitJupiterApi
    testImplementation Dependencies.junitJupiterParams
    testRuntimeOnly Dependencies.junitJupiterEngine
    testRuntimeOnly Dependencies.junitVintageEngine

    testImplementation Dependencies.androidxTest
    testImplementation Dependencies.androidxTestJunit
    testImplementation Dependencies.androidxArchCoreTest
    testImplementation Dependencies.androidxRecyclerview // for performance test
    testImplementation Dependencies.roomTesting
    testImplementation Dependencies.workTesting

    testImplementation Dependencies.coroutinesTest

    testImplementation Dependencies.robolectric

    testImplementation Dependencies.kluent
    testImplementation Dependencies.mockito
    testImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.turbine

    // Instrumentation tests
    androidTestImplementation Dependencies.androidxTestJunit
    androidTestImplementation Dependencies.espressoCore
    androidTestImplementation Dependencies.junit4
    androidTestImplementation Dependencies.kluent
    androidTestImplementation Dependencies.mockito
    androidTestImplementation Dependencies.mockitoKotlin

    detektPlugins(Dependencies.detektFormatting)

    baselineProfile(project(":stream-chat-android-benchmark"))
}
