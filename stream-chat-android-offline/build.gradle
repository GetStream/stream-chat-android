import com.getstream.sdk.chat.Dependencies
import com.getstream.sdk.chat.Configuration

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: "com.hiya.jacoco-android"

group = Configuration.artifactGroup
version = Configuration.versionName

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName Configuration.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'STREAM_CHAT_OFFLINE_VERSION', "\"$version\""
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions.unitTests {
        includeAndroidResources = true
        returnDefaultValues = true
        all {
            testLogging {
                events 'failed', 'standardOut', 'standardError'
                showExceptions true
                showCauses true
                showStackTraces true
                exceptionFormat 'full'
            }
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlinX'
    }
}

kapt {
    arguments {
        arg("room.incremental", "true")
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-progressive',
                '-Xexplicit-api=strict',
                '-Xopt-in=io.getstream.chat.android.client.internal.InternalStreamChatApi',
        ]
    }
}

dependencies {
    api project(":stream-chat-android-client")

    // Kotlin
    implementation Dependencies.kotlinStdLib
    implementation Dependencies.kotlinReflect
    implementation Dependencies.coroutinesCore
    implementation Dependencies.coroutinesAndroid

    // Google libs
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidxCoreKtx
    implementation Dependencies.androidxLifecycleLiveDataKtx
    implementation Dependencies.androidxSecurityCrypto
    implementation Dependencies.firebaseMessaging
    implementation Dependencies.roomRuntime
    implementation Dependencies.roomKtx
    kapt Dependencies.roomCompiler
    implementation Dependencies.workRuntimeKtx

    // Other
    implementation Dependencies.gson

    // Tests
    testImplementation Dependencies.junitJupiterApi
    testImplementation Dependencies.junitJupiterParams
    testRuntimeOnly Dependencies.junitJupiterEngine
    testRuntimeOnly Dependencies.junitVintageEngine

    testImplementation Dependencies.androidxTest
    testImplementation Dependencies.androidxTestJunit
    testImplementation Dependencies.androidxArchCoreTest
    testImplementation Dependencies.androidxRecyclerview // for performance test
    testImplementation Dependencies.roomTesting
    testImplementation Dependencies.workTesting

    testImplementation Dependencies.coroutinesTest

    testImplementation Dependencies.robolectric
    testImplementation Dependencies.dotenv

    testImplementation Dependencies.kfixture
    testImplementation Dependencies.kluent
    testImplementation Dependencies.mockito
    testImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.truth

    // Instrumentation tests
    androidTestImplementation Dependencies.androidxTestJunit
    androidTestImplementation Dependencies.espressoCore
    androidTestImplementation Dependencies.junit4
    androidTestImplementation Dependencies.kluent
    androidTestImplementation Dependencies.mockito
    androidTestImplementation Dependencies.mockitoKotlin
}
