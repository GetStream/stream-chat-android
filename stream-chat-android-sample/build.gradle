import com.getstream.sdk.chat.Dependencies
import com.getstream.sdk.chat.Configuration

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath Dependencies.navigationSafeArgsGradlePlugin
    }
}

android {
    compileSdkVersion Configuration.compileSdkVersion

    defaultConfig {
        applicationId "io.getstream.chat.sample"
        minSdkVersion Configuration.minSdkVersion
        targetSdkVersion Configuration.targetSdkVersion
        versionCode gitVersioner.versionCode
        versionName gitVersioner.versionName
        ndkVersion "21.3.6528147"
    }

    File signFile = rootProject.file('stream-chat-android-sample/.sign/keystore.properties')
    if (signFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signFile))
        signingConfigs {
            release {
                storeFile rootProject.file(properties['keystore'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    } else {
        signingConfigs {
            release {
                storeFile rootProject.file('stream-chat-android-sample/.sign/debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }

    signingConfigs.debug {
        storeFile rootProject.file('stream-chat-android-sample/.sign/debug.keystore')
        storePassword 'android'
        keyAlias 'androiddebugkey'
        keyPassword 'android'
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()

    sourceSets.all {
        it.java.srcDir "src/$it.name/kotlin"
    }

    lintOptions {
        abortOnError false
        xmlReport true
        checkDependencies true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(":stream-chat-android")

    implementation Dependencies.kotlinStdLib

    implementation Dependencies.activityKtx
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidxCoreKtx
    implementation Dependencies.androidxLifecycleExtensions
    implementation Dependencies.androidxPreferences
    implementation Dependencies.constraintLayout
    implementation Dependencies.fragmentKtx
    implementation Dependencies.materialComponents

    implementation Dependencies.navigationRuntimeKTX
    implementation Dependencies.navigationFragmentKTX
    implementation Dependencies.navigationUIKTX

    implementation Dependencies.firebaseAnalytics
    implementation Dependencies.firebaseCrashlytics

    implementation Dependencies.koinAndroidxExt
    implementation Dependencies.koinAndroidxScope
    implementation Dependencies.koinAndroidxViewmodel
    implementation Dependencies.koinCore
    implementation Dependencies.koinCoreExt

    implementation Dependencies.timber

    implementation Dependencies.flipper
    implementation Dependencies.flipperNetwork
    implementation Dependencies.flipperLoader

    implementation Dependencies.glide
    annotationProcessor Dependencies.glideCompiler

    //debugImplementation Dependencies.leakCanary
}
