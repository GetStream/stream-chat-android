apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'maven-publish'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "com.hiya.jacoco-android"

group = 'com.github.getstream'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"
    defaultConfig {

        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.16.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    kotlinOptions {
        jvmTarget = '1.8'
        noStdlib = true
    }

    lintOptions {
        xmlReport false
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-progressive',
                '-Xexplicit-api=strict',
        ]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'


    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.1'

    //Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    //Firebase
    implementation 'com.google.firebase:firebase-messaging:20.2.1'

    //Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'com.github.mrmike:ok2curl:0.6.0'

    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // Tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiter_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiter_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.2.1"
    testImplementation "org.mockito:mockito-core:3.4.6"
    testImplementation "org.json:json:20190722"

    testImplementation 'junit:junit:4.13'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$jupiter_version"
    testImplementation 'org.mockito:mockito-core:3.4.6'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation "androidx.test.ext:junit-ktx:1.1.1"
    testImplementation "androidx.test:core-ktx:1.2.0"
    testImplementation('org.awaitility:awaitility:4.0.2') {
        exclude group: "org.hamcrest", module: "hamcrest"
    }
    testImplementation "org.amshove.kluent:kluent:1.61"
    testImplementation "com.flextrade.jfixture:kfixture:0.2.0"

    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'org.assertj:assertj-core:3.15.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation('org.awaitility:awaitility:4.0.2') {
        exclude group: "org.hamcrest", module: "hamcrest"
    }
}

apply from: 'scripts/docs.gradle'

afterEvaluate {
    // Creates a Maven publication which can be used to test livedata artifact locally.
    // By default the publication is stored into ~/.m2 directory.
    // 1. To release a local publication run `./gradlew publishToMavenLocal`
    // 2. To use it add mavenLocal() into target project's repositories list.
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant
                from components.release
                // Custom attributes of the publication
                groupId = project.group
                artifactId = 'stream-chat-android-client'
                version = '1.16.8-SNAPSHOT'
            }
        }
    }
}
