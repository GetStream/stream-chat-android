import io.getstream.chat.android.Dependencies
import io.getstream.chat.android.command.dag.ktlint.plugin.SelectedKtlintPlugin
import io.getstream.chat.android.command.dag.unittest.plugin.SelectedUnitTestsPlugin

apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'
apply from: "${rootDir}/scripts/sample-app-versioner.gradle"
apply plugin: SelectedKtlintPlugin
apply plugin: SelectedUnitTestsPlugin
apply plugin: 'com.github.leandroborgesferreira.dag-command'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        mavenCentral()
    }

    dependencies {
        classpath Dependencies.androidGradlePlugin
        classpath Dependencies.kotlinGradlePlugin
        classpath Dependencies.googleServicesPlugin
        classpath Dependencies.androidJunit5GradlePlugin
        classpath Dependencies.gitversionerPlugin
        classpath Dependencies.firebaseCrashlyticsPlugin
        classpath Dependencies.ktlintPlugin
        classpath Dependencies.gradleVersionsPlugin
        classpath Dependencies.navigationSafeArgsGradlePlugin
        classpath Dependencies.gradleNexusPublishPlugin
        classpath Dependencies.dokka
        classpath Dependencies.kotlinBinaryValidator
        classpath Dependencies.dagCommand
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
            content {
                includeModule("com.github.chrisbanes", "PhotoView")
                includeModule("com.github.duanhong169", "drawabletoolbox")

                // https://github.com/brianwernick/ExoMedia/issues/723
                includeModule("com.github.brianwernick", "ExoMedia")

                // https://github.com/stfalcon-studio/StfalconImageViewer/issues/69
                includeModule("com.github.stfalcon-studio", "StfalconImageViewer")
            }
        }
    }
}

dagCommand {
    filter = "all"
    defaultBranch = System.getenv('GITHUB_BASE_REF') ?: "develop"
    outputType = "json"
    printModulesInfo = true
}

selectedKtlint {
    changeModulesPath = "${rootProject.buildDir}/dag-command/affected-modules.json"
    outputPath = "kt-selected-command.sh"
}

selectedTestDebug {
    changeModulesPath = "${rootProject.buildDir}/dag-command/affected-modules.json"
    outputPath = "unit-tests-selected-command.sh"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates {
    rejectVersionIf {
        Dependencies.isStable(it.currentVersion) && Dependencies.isNonStable(it.candidate.version)
    }
}

apiValidation {
    ignoredPackages += [
            'com/getstream/sdk/chat/databinding',
            'io/getstream/chat/android/ui/databinding',
    ]

    ignoredProjects += [
            'stream-chat-android-docs',
            'stream-chat-android-sample',
            'stream-chat-android-ui-components-sample',
            'stream-chat-android-test',
            'stream-chat-android-compose-sample',
    ]

    nonPublicMarkers += [
            'io.getstream.chat.android.core.internal.InternalStreamChatApi',
    ]
}

apply from: "${rootDir}/scripts/publish-root.gradle"
