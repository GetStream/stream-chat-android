import com.getstream.sdk.chat.Dependencies

apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'
apply from: "${rootDir}/scripts/sample-app-versioner.gradle"

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        mavenCentral()
    }

    dependencies {
        classpath Dependencies.androidGradlePlugin
        classpath Dependencies.kotlinGradlePlugin
        classpath Dependencies.googleServicesPlugin
        classpath Dependencies.androidJunit5GradlePlugin
        classpath Dependencies.gitversionerPlugin
        classpath Dependencies.firebaseCrashlyticsPlugin
        classpath Dependencies.ktlintPlugin
        classpath Dependencies.gradleVersionsPlugin
        classpath Dependencies.navigationSafeArgsGradlePlugin
        classpath Dependencies.gradleNexusPublishPlugin
        classpath Dependencies.dokka
        classpath Dependencies.kotlinBinaryValidator
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
            content {
                includeModule("com.github.chrisbanes", "PhotoView")
                includeModule("com.github.duanhong169", "drawabletoolbox")

                // https://github.com/mrmike/Ok2Curl/issues/60
                includeModule("com.github.mrmike", "ok2curl")

                // https://github.com/brianwernick/ExoMedia/issues/723
                includeModule("com.github.brianwernick", "ExoMedia")

                // https://github.com/stfalcon-studio/StfalconImageViewer/issues/69
                includeModule("com.github.stfalcon-studio", "StfalconImageViewer")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Task to generate docusaurus sidebar. Uncomment if dokka for docusaurus is desired.
//tasks.register('docusaurusSidebar', GenerateSidebar) {
//    inputDir = file("build/dokka/docusaurus")
//    outputDir = file("docusaurus/docs/Android/Dokka")
//    modulesFilterInput = [
//            'stream-chat-android-client',
//            'stream-chat-android-offline',
//            'stream-chat-android-ui-components',
//    ]
//    removeFromLabels = "io.getstream.chat.android."
//    simplifyOutput = true
//}

dependencyUpdates {
    rejectVersionIf {
        Dependencies.isStable(it.currentVersion) && Dependencies.isNonStable(it.candidate.version)
    }
}

apiValidation {
    ignoredPackages += [
            'com/getstream/sdk/chat/databinding',
            'io/getstream/chat/android/ui/databinding',
    ]

    ignoredProjects += [
            'stream-chat-android-docs',
            'stream-chat-android-sample',
            'stream-chat-android-ui-components-sample',
            'stream-chat-android-test',
    ]

    nonPublicMarkers += [
            'io.getstream.chat.android.core.internal.InternalStreamChatApi',
    ]
}

apply from: "${rootDir}/scripts/publish-root.gradle"
