import io.getstream.chat.android.Dependencies
import io.getstream.chat.android.Versions
import io.getstream.chat.android.command.release.plugin.ReleasePlugin
import io.getstream.chat.android.command.unittest.plugin.UnitTestsPlugin

apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'
apply from: "${rootDir}/scripts/sample-app-versioner.gradle"
apply plugin: UnitTestsPlugin
apply plugin: ReleasePlugin

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        mavenCentral()
        maven {
            url 'https://developer.huawei.com/repo/'
            content {
                includeGroup("com.huawei.agconnect")
            }
        }
    }

    dependencies {
        classpath Dependencies.androidGradlePlugin
        classpath Dependencies.kotlinGradlePlugin
        classpath Dependencies.googleServicesPlugin
        classpath Dependencies.androidJunit5GradlePlugin
        classpath Dependencies.gitversionerPlugin
        classpath Dependencies.firebaseCrashlyticsPlugin
        classpath Dependencies.ktlintPlugin
        classpath Dependencies.gradleVersionsPlugin
        classpath Dependencies.navigationSafeArgsGradlePlugin
        classpath Dependencies.gradleNexusPublishPlugin
        classpath Dependencies.dokka
        classpath Dependencies.kotlinBinaryValidator
        classpath Dependencies.huaweiPlugin
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10'
//        classpath Dependencies.shot
    }
}

subprojects {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    ktlint {
        version = Versions.KTLINT_VERSION
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
            content {
                includeModule("com.github.chrisbanes", "PhotoView")
                includeModule("com.github.duanhong169", "drawabletoolbox")

                // https://github.com/brianwernick/ExoMedia/issues/723
                includeModule("com.github.brianwernick", "ExoMedia")

                // https://github.com/stfalcon-studio/StfalconImageViewer/issues/69
                includeModule("com.github.stfalcon-studio", "StfalconImageViewer")
            }
        }
    }
}

unitTestDebugScript {
    outputPath = "unit-tests-command.sh"
}

releaseScript {
    changelogPath = "CHANGELOG.md"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates {
    rejectVersionIf {
        Dependencies.isStable(it.currentVersion) && Dependencies.isNonStable(it.candidate.version)
    }
}

apiValidation {
    ignoredPackages += [
            'com/getstream/sdk/chat/databinding',
            'io/getstream/chat/android/ui/databinding',
    ]

    ignoredProjects += [
            'stream-chat-android-docs',
            'stream-chat-android-ui-components-sample',
            'stream-chat-android-test',
            'stream-chat-android-compose-sample',
    ]

    nonPublicMarkers += [
            'io.getstream.chat.android.core.internal.InternalStreamChatApi',
    ]
}

apply from: "${rootDir}/scripts/publish-root.gradle"
