import com.getstream.sdk.chat.Dependencies

apply plugin: 'com.pascalwelsch.gitversioner'
apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        mavenCentral()
    }

    dependencies {
        classpath Dependencies.androidGradlePlugin
        classpath Dependencies.kotlinGradlePlugin
        classpath Dependencies.googleServicesPlugin
        classpath Dependencies.androidMavenGradlePlugin
        classpath Dependencies.androidJunit5GradlePlugin
        classpath Dependencies.gitversionerPlugin
        classpath Dependencies.firebasePlugin
        classpath Dependencies.ktlintPlugin
        classpath Dependencies.gradleVersionsPlugin
        classpath Dependencies.navigationSafeArgsGradlePlugin
        classpath Dependencies.gradleNexusStagingPlugin
        classpath Dependencies.dokka
        classpath Dependencies.kotlinBinaryValidator
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        jcenter() {
            content {
                // Remove when https://github.com/google/ExoPlayer/issues/5246 is closed
                includeGroup("com.google.android.exoplayer")

                // Remove when https://github.com/ongakuer/PhotoDraweeView/issues/81 is closed
                includeModule("me.relex", "photodraweeview")

                // https://youtrack.jetbrains.com/issue/IDEA-261387
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
        maven {
            url "https://jitpack.io"
            content {
                includeModule("com.github.chrisbanes", "PhotoView")
                includeModule("com.github.duanhong169", "drawabletoolbox")

                // Remove when https://github.com/mrmike/Ok2Curl/issues/60 is closed
                includeModule("com.github.mrmike", "ok2curl")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

gitVersioner {
    def branches = ['develop', 'main']
    baseBranch branchName in branches ? branchName : branches[0]
    yearFactor 100
    formatter = { gitVersioner ->
        "1.${gitVersioner.versionCode}.${gitVersioner.featureBranchCommitCount}.${gitVersioner.currentSha1Short}"
    }
}

dependencyUpdates {
    rejectVersionIf {
        Dependencies.isStable(it.currentVersion) && Dependencies.isNonStable(it.candidate.version)
    }
}

apiValidation {
    ignoredPackages += [
            'com/getstream/sdk/chat/databinding',
            'io/getstream/chat/android/ui/databinding',
    ]

    ignoredProjects += [
            'stream-chat-android-docs',
            'stream-chat-android-sample',
            'stream-chat-android-ui-components-sample',
            'stream-chat-android-test',
    ]

    nonPublicMarkers += [
            'io.getstream.chat.android.core.internal.InternalStreamChatApi',
    ]
}
