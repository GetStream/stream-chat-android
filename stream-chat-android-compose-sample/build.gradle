import io.getstream.chat.android.Dependencies

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.baselineprofile'

apply from: "${rootDir}/scripts/android.gradle"
apply from: "${rootDir}/scripts/detekt-compose.gradle"

android {
    namespace "io.getstream.chat.android.compose.sample"
    defaultConfig {
        targetSdk io.getstream.chat.android.Configuration.sampleTargetSdk
        applicationId "io.getstream.chat.android.compose.sample"
        versionCode sampleAppVersionCode
        versionName sampleAppVersionName
        testInstrumentationRunner = "io.qameta.allure.android.runners.AllureAndroidJUnitRunner"
        testInstrumentationRunnerArguments["clearPackageData"] = "true"
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    File signFile = rootProject.file('.sign/keystore.properties')
    if (signFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signFile))
        signingConfigs {
            release {
                storeFile rootProject.file(properties['keystore'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    } else {
        signingConfigs {
            release {
                storeFile rootProject.file('.sign/debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }

    signingConfigs.debug {
        storeFile rootProject.file('.sign/debug.keystore')
        storePassword 'android'
        keyAlias 'androiddebugkey'
        keyPassword 'android'
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            buildConfigField "Boolean", "BENCHMARK", "false"
        }
        benchmark {
            debuggable false
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
            proguardFiles 'benchmark-rules.pro'
            buildConfigField "Boolean", "BENCHMARK", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "Boolean", "BENCHMARK", "false"
        }
    }

    flavorDimensions "version"
    productFlavors {
        demo {
            dimension "version"
        }
        e2e {
            dimension "version"
            applicationIdSuffix ".e2etest"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (buildType.name != "debug" && names.contains("e2e")) {
            setIgnore(true)
        }
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-opt-in=kotlin.RequiresOptIn',
        ]
    }
}

dependencies {
    implementation project(':stream-chat-android-compose')
    implementation project(":stream-chat-android-offline")

    implementation Dependencies.androidxCoreKtx
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.materialComponents
    implementation Dependencies.streamPushFirebase
    implementation Dependencies.streamLog

    // Compose
    implementation Dependencies.composeUi
    implementation Dependencies.composeUiTooling
    implementation Dependencies.composeFoundation
    implementation Dependencies.composeMaterial3
    implementation Dependencies.composeMaterialIcons

    implementation Dependencies.composeActivity
    implementation Dependencies.composeAndroidLifecycle
    implementation Dependencies.composeViewModel
    implementation Dependencies.composeAccompanistPermissions
    implementation Dependencies.composeAccompanistPager

    // Coil
    implementation Dependencies.composeLandscapistCoil

    // Firebase
    implementation Dependencies.firebaseAnalytics
    implementation Dependencies.firebaseCrashlytics

    // Instrumentation tests
    androidTestImplementation(Dependencies.androidxTestRunner)
    androidTestImplementation(Dependencies.androidxUiAutomator)
    androidTestImplementation(Dependencies.androidxTestJunitKtx)
    androidTestImplementation(Dependencies.androidxTestMonitor)
    androidTestUtil(Dependencies.androidxTestOrchestrator)
    androidTestImplementation(Dependencies.allureKotlinModel)
    androidTestImplementation(Dependencies.allureKotlinJunit)
    androidTestImplementation(Dependencies.allureKotlinCommons)
    androidTestImplementation(Dependencies.allureKotlinAndroid)
    androidTestImplementation(project(":stream-chat-android-e2e-test"))

    detektPlugins(Dependencies.detektFormatting)

    baselineProfile(project(":stream-chat-android-benchmark"))
}
