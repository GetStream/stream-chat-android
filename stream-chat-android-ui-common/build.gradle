import io.getstream.chat.android.Dependencies
import io.getstream.chat.android.Configuration
import io.getstream.chat.android.Versions

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'de.mannodermaus.android-junit5'

ext {
    PUBLISH_GROUP_ID = Configuration.artifactGroup
    PUBLISH_ARTIFACT_ID = 'stream-chat-android-ui-common'
    PUBLISH_VERSION = rootVersionName
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android.gradle"

android {
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    resourcePrefix 'stream_'

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
            // Show the result of every unit test, even if it passes.
            all {
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
        }
    }

    sourceSets {
        all {
            it.java.srcDir "src/$it.name/kotlin"
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.ANDROIDX_COMPOSE_COMPILER
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-progressive',
                '-Xexplicit-api=strict',
                '-opt-in=io.getstream.chat.android.core.internal.InternalStreamChatApi'
        ]
    }
}

dependencies {
    api project(":stream-chat-android-state")
    implementation project(":stream-chat-android-ui-utils")

    implementation Dependencies.streamLog
    implementation Dependencies.composeCompiler
    implementation Dependencies.composeRuntime

    implementation Dependencies.androidxActivityKtx
    implementation Dependencies.androidxAnnotations
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidxRecyclerview
    implementation Dependencies.androidxStartup
    implementation Dependencies.constraintLayout
    implementation Dependencies.materialComponents
    implementation Dependencies.androidxLifecycleLiveDataKtx

    implementation Dependencies.coil
    implementation Dependencies.coilGif
    implementation Dependencies.coilVideo

    testImplementation project(":stream-chat-android-test")
    testImplementation Dependencies.kotlinReflect
    testImplementation Dependencies.junitJupiterApi
    testImplementation Dependencies.junitJupiterParams
    testRuntimeOnly Dependencies.junitJupiterEngine
    testRuntimeOnly Dependencies.junitVintageEngine

    testImplementation Dependencies.threeTenBp

    testImplementation Dependencies.androidxArchCoreTest
    testImplementation Dependencies.testParameterInjector
    testImplementation Dependencies.kluent
    testImplementation Dependencies.mockito
    testImplementation Dependencies.mockitoKotlin
    testImplementation Dependencies.coroutinesTest

    detektPlugins(Dependencies.detektFormatting)
}
